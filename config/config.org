#+TITLE: Personal Emacs Configuration
#+AUTHOR: Gil Tomás

* Notes
Adapted from the config.org files available [[https://github.com/danielmai/.emacs.d/blob/master/config.org][here]] and [[https://github.com/nasseralkmim/.emacs.d/blob/master/config.org][here]].

This configuration uses the [[https://github.com/jwiegley/use-package][use-package]] package from John Wiegley, which is a
fantastic way to manage package configurations. This [[http://www.lunaryorn.com/2015/01/06/my-emacs-configuration-with-use-package.html][post]] explain how it works.

This [[http://emacs.stackexchange.com/questions/10396/difference-between-init-and-config-in-use-package][Question]] is useful to understand the differences between init and config.
Basically, bind and init are evaluated immediately after start up which
contributes to startup time, config is deferred until the pacakge is loaded (by
a bind key or a specific mode).

* Personal information
#+BEGIN_SRC emacs-lisp
  (setq user-full-name "Gil Tomás"
        user-mail-address "gil.tms@gmail.com")
#+END_SRC

* Loading config.org

Using the method described [[http://www.holgerschurig.de/en/emacs-efficiently-untangling-elisp/][here]].

*** Package management
#+BEGIN_SRC emacs-lisp
  (package-initialize nil)
  (setq package-enable-at-startup nil)
#+END_SRC

#+BEGIN_SRC emacs-lisp
;;; Set up package
;; initalize all ELPA packages
(require 'package)
(setq package-enable-at-startup nil
      package-archives
      '(("melpa"        . "http://melpa.org/packages/")
        ("melpa-stable" . "http://stable.melpa.org/packages/")
        ("RSW-packages" . "http://www.plasmas.biz/rswe/")))
#+END_SRC
*** use-package

See [http://github.com/jwiegley/use-package/]
or http://www.lunaryorn.com/2015/01/06/my-emacs-configuration-with-use-package.html

Basic syntax:

#+BEGIN_EXAMPLE
(use-package package-name
         [:keyword [option]]...)

:init          Code to run before PACKAGE-NAME has been loaded.
:config        Code to run after PACKAGE-NAME has been loaded.  Note that if
                           loading is deferred for any reason, this code does not execute
                           until the lazy load has occurred.
:preface       Code to be run before everything except `:disabled'; this can
                           be used to define functions for use in `:if', or that should be
                           seen by the byte-compiler.
:mode          Form to be added to `auto-mode-alist'.
:interpreter   Form to be added to `interpreter-mode-alist'.
:commands      Define autoloads for commands that will be defined by the
                           package.  This is useful if the package is being lazily loaded,
                           and you wish to conditionally call functions in your `:init'
                           block that are defined in the package.
:bind          Bind keys, and define autoloads for the bound commands.
:bind*         Bind keys, and define autoloads for the bound commands,
                           *overriding all minor mode bindings*.
:bind-keymap   Bind a key prefix to an auto-loaded keymap defined in the
                           package.  This is like `:bind', but for keymaps.
:bind-keymap*  Like `:bind-keymap', but overrides all minor mode bindings
:defer         Defer loading of a package -- this is implied when using
                           `:commands', `:bind', `:bind*', `:mode' or `:interpreter'.
                           This can be an integer, to force loading after N seconds of
                           idle time, if the package has not already been loaded.
:after         Defer loading of a package until after any of the named
                           features are loaded.
:demand        Prevent deferred loading in all cases.
:if EXPR       Initialize and load only if EXPR evaluates to a non-nil value.
:disabled      The package is ignored completely if this keyword is present.
:defines       Declare certain variables to silence the byte-compiler.
:functions     Declare certain functions to silence the byte-compiler.
:load-path     Add to the `load-path' before attempting to load the package.
:diminish      Support for diminish.el (if installed).
:ensure        Loads the package using package.el if necessary.
:pin           Pin the package to an archive.
#+END_EXAMPLE

#+BEGIN_SRC emacs-lisp
(unless (package-installed-p 'use-package)
  (package-refresh-contents)
  (package-install 'use-package))

(eval-when-compile
  (require 'use-package))
(require 'diminish)                ;; if you use :diminish
(require 'bind-key)
;(setq use-package-verbose t)
#+END_SRC

* Custom settings
#+BEGIN_SRC emacs-lisp
  (setq custom-file (expand-file-name "custom.el"
  prelude-user-init-file))
  (load custom-file)
#+END_SRC

* Global Key Bindings
#+BEGIN_SRC emacs-lisp
  (global-set-key (kbd "C-x o") 'ace-window)
#+END_SRC

* Prelude Hacks
#+BEGIN_SRC emacs-lisp
  ;; cancels prelude-ui.el instruction to render the fringe (gutter) smaller the
  ;; argument is a width in pixels (the default is 8)
  (if (fboundp 'fringe-mode)
      (fringe-mode 8))
  ;; proced was replaced by vkill on prelude: disable vkill instead
  ;; 29Jul2014
  (put 'vkill 'disabled t)
  ;; rebinds C-- and C-+ to negative and positive arguments respectively,
  ;; overriding code on prelude-global-keybindings.el (previously bound to
  ;; text-scale-increase/decrease)
  ;; 03Aug2015
  (global-set-key (kbd "C--") 'negative-argument)
  (global-set-key (kbd "C-+") 'positive-argument)
#+END_SRC

* Font

Download

| [[https://github.com/adobe-fonts/source-code-pro][Source Code Pro]]     |
| [[https://www.fontsquirrel.com/fonts/download/dejavu-sans][Dejavu Sans]]         |
| [[https://fontlibrary.org/pt/font/fantasque-sans-mono][Fantasque Sans mono]] |
| [[http://leonardo-m.livejournal.com/77079.html][Inconsolata g]]       |

#+begin_src emacs-lisp
(set-frame-font "Source Code Pro 14")
;; (set-frame-font "Monospace 10")
;; (set-frame-font "Dejavu Sans 10")
;; (set-frame-font "Fantasque Sans Mono 11")
;; (set-frame-font "Inconsolata-g 10")
#+end_src

* Common defaults

Sources for this section include [[https://github.com/magnars/.emacs.d/blob/master/settings/sane-defaults.el][Magnars Sveen]] and [[http://pages.sachachua.com/.emacs.d/Sacha.html][Sacha Chua]].

#+BEGIN_SRC emacs-lisp
      ;; These functions are useful. Activate them.
      (put 'downcase-region 'disabled nil)
      (put 'upcase-region 'disabled nil)
      (put 'narrow-to-region 'disabled nil)
      (put 'dired-find-alternate-file 'disabled nil)

      ;; Answering just 'y' or 'n' will do
      (defalias 'yes-or-no-p 'y-or-n-p)

      ;; UTF-8 please
      (set-language-environment "UTF-8")
      (setq locale-coding-system 'utf-8) ; pretty
      (set-terminal-coding-system 'utf-8) ; pretty
      (set-keyboard-coding-system 'utf-8) ; pretty
      (set-selection-coding-system 'utf-8) ; please
      (prefer-coding-system 'utf-8) ; with sugar on top
      (setq default-buffer-file-coding-system 'utf-8)
      (setq x-select-request-type '(UTF8_STRING COMPOUND_TEXT TEXT STRING))
      ;; from Sacha page
      (when (display-graphic-p)
        (setq x-select-request-type '(UTF8_STRING COMPOUND_TEXT TEXT STRING)))
      (setq-default indent-tabs-mode nil)

      (setq uniquify-buffer-name-style 'forward)

      ;; Don't beep at me
      (setq visible-bell t)

    ;; the following is copied from my previous personal.el
    ;; 21Sep2014
    ;; toggles delete-selection-mode on
    (delete-selection-mode 1)
    ;; toggles display-time mode on
    (setq display-time-day-and-date t)
    (display-time)
    ;; removes all scroll bars
    (scroll-bar-mode -1)
    ;; configures emacs so that word moving commands will move cursor into
    ;; between CamelCaseWords
    (global-subword-mode 1)
    ;; sets set-mark-command-repeat-pop to TRUE
    ;; non-nil means repeating C-SPC after popping mark pops it again
    (setq set-mark-command-repeat-pop t)
    ;; defines variable dired-listing-switches
    (setq dired-listing-switches "-alh")
    ;; defines variable orgstruct-heading-prefix-regexp
    (defvar orgstruct-heading-prefix-regexp)
    (setq orgstruct-heading-prefix-regexp "## ")
  ;; The right margin is determined by the variable fill-column.  The default
  ;; value for fill-column is 70; here we set it to 80.  21Sep2016
  (setq-default fill-column 80)
#+END_SRC

Here we make page-break characters look pretty, instead of appearing
as =^L= in Emacs. [[http://ericjmritz.name/2015/08/29/using-page-breaks-in-gnu-emacs/][Here's an informative article called "Using
Page-Breaks in GNU Emacs" by Eric J. M. Ritz.]]

#+begin_src emacs-lisp
(use-package page-break-lines
  :ensure t)
#+end_src

* Org mode
** Org itself
   #+BEGIN_SRC emacs-lisp
     (use-package org
       :ensure t
       :mode (("\\.org$" . org-mode))
       :bind(("C-c a" . org-agenda)
             ("C-c l" . org-store-link)
             ("C-c c" . org-capture))
       :config
       (add-hook 'org-mode-hook 'smartparens-mode)
       (add-hook 'org-mode-hook 'rainbow-delimiters-mode)
       (add-hook 'org-mode-hook 'company-mode)
       (add-hook 'org-mode-hook 'flyspell-mode)
       (add-hook 'org-mode-hook 'turn-on-org-cdlatex)

       (use-package org-bullets
         :ensure t
         :init
         (add-hook 'org-mode-hook (lambda () (org-bullets-mode 1)))
         ;; (setq org-ellipsis " …")
         (setq org-bullets-bullet-list '("•")))

        ;; beamer export
       (eval-after-load "org"
         '(require 'ox-beamer))

       (use-package ox-reveal :ensure ox-reveal
         :config
         (setq org-reveal-root "http://cdn.jsdelivr.net/reveal.js/3.0.0/")
         (setq org-reveal-mathjax t))

       ;; For code fragments typesetting
       ;; http://orgmode.org/worg/org-tutorials/org-latex-preview.html
       (setq org-latex-listings 'minted)
       (require 'ox-latex)
       ;; code highlight for latex org export
       (add-to-list 'org-latex-packages-alist '("" "minted"))
       ;; add a nice font to org latex export
       (add-to-list 'org-latex-packages-alist '("" "libertine"))

       ;; preview latex in org mode with imagemagick
       (setq org-latex-create-formula-image-program 'imagemagick)

       ;; Org babel and source blocks
       (setq org-src-fontify-natively t
             org-src-window-setup 'current-window
             org-src-strip-leading-and-trailing-blank-lines t
             org-src-preserve-indentation t
             org-src-tab-acts-natively t
             org-export-babel-evaluate nil
             org-confirm-babel-evaluate nil) ; doesn't ask for confirmation

       ;;; display/update images in the buffer after I evaluate
       (add-hook 'org-babel-after-execute-hook 'org-display-inline-images 'append)

       ;; This is for remove the annoying background color on the headings, level 1 and level 2, when using the material-theme.
       (custom-set-faces
       '(org-level-1 ((t (:background nil :bold t :overline nil))))
       '(org-level-2 ((t (:background nil :bold t :overline nil)))))

       (setq org-modules '(org-habit))

       (eval-after-load 'org
         '(org-load-modules-maybe t)))
   #+END_SRC

** Agenda and Capture files

   Set agenda files.

   remove done tasks from the agenda, from [[http://stackoverflow.com/questions/8281604/remove-done-tasks-from-agenda-view][this question]].

   Set gtd.org as my default notes file

   From [[http://sachachua.com/blog/2015/02/learn-take-notes-efficiently-org-mode/][This blog entry]], C-c o is set to open the tasks.org

   #+BEGIN_SRC emacs-lisp
     (use-package org-agenda
       :defer t
       :config
       (setq org-agenda-files
             (delq nil
                   (mapcar (lambda (x) (and (file-exists-p x) x))
                           '("~/Dropbox/org/gtd.org"
                             "~/Dropbox/org/notes.org"
                             "~/Dropbox/org/culture.org"))))
        (custom-set-variables
       '(org-agenda-skip-scheduled-if-done t)
       '(org-agenda-skip-deadline-if-done t))

        (setq org-default-notes-file "~/Dropbox/org/gtb.org"))
   #+END_SRC

** Org babel/source blocks

I like to have source blocks properly syntax highlighted and with the
editing popup window staying within the same window so all the windows
don't jump around. Also, having the top and bottom trailing lines in
the block is a waste of space, so we can remove them.

I noticed that fontification doesn't work with markdown mode when the
block is indented after editing it in the org src buffer---the leading
#s for headers don't get fontified properly because they appear as Org
comments. Setting ~org-src-preserve-indentation~ makes things
consistent as it doesn't pad source blocks with leading spaces.

#+begin_src emacs-lisp
(setq org-src-fontify-natively t
      org-src-window-setup 'current-window
      org-src-strip-leading-and-trailing-blank-lines t
      org-src-preserve-indentation t
      org-src-tab-acts-natively t)
#+end_src

* ELPA packages
** AucTex
*** Basic setup
Enable
[[https://www.gnu.org/software/auctex/manual/auctex.html][Auctex]]
when files with .tex are loaded. Tex-master is useful to make Auctex
aware of multi-files documents. TeX-PDF mode is used to compile using
pdflatex.

#+begin_src emacs-lisp
(use-package tex-site
  :ensure auctex
  :mode ("\\.tex\\'" . latex-mode)
  :config
  (setq TeX-auto-save t)
  (setq TeX-parse-self t)
  (setq-default TeX-master nil)
  (add-hook 'LaTeX-mode-hook
            (lambda ()
              (magic-latex-buffer)
              (LaTeX-math-mode)
              (rainbow-delimiters-mode)
              (flyspell-mode)
              (company-mode)
              (smartparens-mode)
              (turn-on-reftex)
              (setq reftex-plug-into-AUCTeX t)
              (reftex-isearch-minor-mode)
              (setq TeX-PDF-mode t)
              (setq global-font-lock-mode t)
              (setq TeX-source-correlate-method 'synctex)
              (setq TeX-source-correlate-start-server t)))

;https://github.com/politza/pdf-tools/issues/187

(add-hook 'TeX-after-compilation-finished-functions #'TeX-revert-document-buffer)

;; to use pdfview with auctex
(add-hook 'LaTeX-mode-hook 'pdf-tools-install)
;; nil beacuse I don't want the pdf to be opened again in the same frame after C-c C-a
;; (setq TeX-view-program-selection nil)
;; (setq TeX-view-program-selection '((output-pdf "pdf-tools")))
;; (setq TeX-view-program-list '(("pdf-tools" "TeX-pdf-tools-sync-view")))

;; https://github.com/politza/pdf-tools/pull/60
(setq pdf-sync-forward-display-action
      '(display-buffer-reuse-window (reusable-frames . t)))
;; same thing, now I can jump from pdf in another frame into source
(setq pdf-sync-backward-display-action
      '(display-buffer-reuse-window (reusable-frames . t)))

; language specific hooks in auctex
(add-hook 'TeX-language-dk-hook
      (lambda () (ispell-change-dictionary "brasileiro"))))
#+end_src

*** Company auctex

From [[https://github.com/manugoyal/.emacs.d#latex][here]].

#+BEGIN_SRC emacs-lisp
(use-package company-auctex
  :ensure t
  :defer t
  :config
  (company-auctex-init))
#+END_SRC

*** Latex preview pane

shell-escape mode because the pacakge svg requires it.

#+BEGIN_SRC emacs-lisp
(use-package latex-preview-pane
  :disabled t
  :bind ("M-p" . latex-preview-pane-mode)
  :config
  (setq doc-view-ghostscript-program "gswin64c")

  (custom-set-variables
   '(shell-escape-mode "-shell-escape")
   '(latex-preview-pane-multifile-mode (quote auctex))))
#+END_SRC
** Company
Auto complete

The configurations were taken from [[https://github.com/company-mode/company-mode/issues/68][This discussion]]. The delay time from the
wiki. This [[https://www.reddit.com/r/emacs/comments/3s5bkf/companymode_configuration_make_editing_slow/][reddit thread]] has some cool configurations. [[https://github.com/manugoyal/.emacs.d#company-mode][Company capf is
problematic?]]

#+BEGIN_SRC emacs-lisp
  (use-package company
    :ensure t
    :diminish company-mode
    :defer t
    :config
    (add-hook 'after-init-hook 'global-company-mode)
    (setq company-idle-delay 0)
    (setq company-show-numbers t)
    (setq company-minimum-prefix-length 3)
    (delete 'company-capf company-backends))
#+END_SRC

** Hungry Delete
  #+BEGIN_SRC emacs-lisp
    (use-package hungry-delete
                 :diminish hungry-delete
                 :defer t
                 :config
                 (global-hungry-delete-mode)
                 (global-set-key (kbd "C-<delete>") 'hungry-delete-forward)
                 (global-set-key (kbd "C-<backspace>") 'hungry-delete-backward))
  #+END_SRC
** Ido Vertical Mode
#+BEGIN_SRC emacs-lisp
(use-package ido-vertical-mode
  :ensure t
  :diminish ido-vertical-mode
  :config
  (ido-mode 1)
  (ido-vertical-mode 1)
  (setq ido-vertical-define-keys 'C-n-and-C-p-only))
#+END_SRC

** Magic Latex Buffer
   #+begin_src emacs-lisp
(use-package magic-latex-buffer
  :config
  (add-hook 'LaTeX-mode-hook 'magic-latex-buffer)
  (setq magic-latex-enable-block-highlight nil
      magic-latex-enable-suscript        t
      magic-latex-enable-pretty-symbols  t
      magic-latex-enable-block-align     nil
      magic-latex-enable-inline-image    nil))
   #+end_src

** Pdf tools

-From the larslj's  [[https://github.com/larslj/pdf-tools/tree/windows][windows branch]].-

#+BEGIN_SRC emacs-lisp
(use-package pdf-tools
  :ensure t
  :mode ("\\.pdf\\'" . pdf-tools-install)
  :bind ("C-c C-g" . pdf-sync-forward-search)
  :defer t
  :config
  (setq mouse-wheel-follow-mouse t)
  (setq pdf-view-resize-factor 1.10))
#+END_SRC

** Reftex

[[http://www.gnu.org/software/auctex/manual/reftex.html#SEC2][RefTex website]]. Useful for managing cross references, bibliographies, indices,
and document navigation.

#+BEGIN_SRC emacs-lisp
  (use-package reftex
    :ensure t
    :defer t
    :config
    (setq reftex-cite-prompt-optional-args t)); Prompt for empty
                                              ; optional arguments in
                                              ; cite
#+END_SRC

** Relative Line Numbers
#+BEGIN_SRC emacs-lisp
  (use-package relative-line-numbers
    :defer t)
#+END_SRC

** Smartparens
#+BEGIN_SRC emacs-lisp
  (use-package smartparens
    :ensure t
    :defer t
    :diminish smartparens-mode
    :config
    (smartparens-global-mode)
    (show-smartparens-global-mode t)
    (sp-local-pair 'org-mode "_" "_" )
    (sp-local-pair 'org-mode "*" "*" )
    (sp-local-pair 'latex-mode "$" "$" )
    (sp-local-pair 'latex-mode "\\left(" "\\right)" :trigger "\\l("))
#+END_SRC

** Writegood mode

From [[https://github.com/grettke/help/blob/master/help.org][this source]]

#+BEGIN_SRC emacs-lisp :tangle no
(use-package writegood-mode
  :ensure t
  :config
  (eval-after-load "writegood-mode"
    '(diminish 'writegood-mode)))
#+END_SRC

* Misc
** Doc view keybindings
#+begin_src emacs-lisp :tangle no
  (use-package doc-view
    :config
    (add-hook 'doc-view-mode-hook (lambda () (centered-cursor-mode -1)))
    (define-key doc-view-mode-map (kbd "<right>") 'doc-view-next-page)
    (define-key doc-view-mode-map (kbd "<left>") 'doc-view-previous-page)
    (setq mouse-wheel-progressive-speed nil) ;; don't accelerate scrolling
    (setq mouse-wheel-follow-mouse 't) ;; scroll window under mouse

    (global-set-key (kbd "C-<wheel-up>") 'doc-view-enlarge)
    (global-set-key (kbd "C-<wheel-down>") 'doc-view-shrink)

    (setq doc-view-continuous t))
#+end_src

** Remove ad-handle message
#+BEGIN_SRC emacs-lisp
(setq ad-redefinition-action 'accept)
#+END_SRC

** Change src block bg
#+BEGIN_SRC emacs-lisp
  (with-eval-after-load 'org
          ;; * Colored src blocks
          ;; based on patches from Rasmus <rasmus@gmx.us>

          ;; This function overwrites the org-src function to make src blocks be colored again.
          (defun org-src-font-lock-fontify-block (lang start end)
            "Fontify code block.
          LANG is the language of the block.  START and END are positions of
          the block.  This function is called by Emacs automatic
          fontification, as long as `org-src-fontify-natively' is non-nil."
            (let ((lang-mode (org-src--get-lang-mode lang)))
              (when (fboundp lang-mode)
                (let ((string (buffer-substring-no-properties start end))
                      (modified (buffer-modified-p))
                      (org-buffer (current-buffer))
                      (block-faces (let ((face-name (intern (format "org-block-%s" lang))))
                                     (append (and (facep face-name) (list face-name))
                                             '(org-block)))))
                  (remove-text-properties start end '(face nil))
                  (with-current-buffer
                      (get-buffer-create
                       (format " *org-src-fontification:%s*" lang-mode))
                    (erase-buffer)
                    (insert string " ") ;; so there's a final property change
                    (unless (eq major-mode lang-mode) (funcall lang-mode))
                    (org-font-lock-ensure)
                    (let ((pos (point-min)) next)
                      (while (setq next (next-single-property-change pos 'face))
                        (let ((new-face (get-text-property pos 'face)))
                          (put-text-property
                           (+ start (1- pos)) (1- (+ start next)) 'face
                           (list :inherit (append (and new-face (list new-face))
                                                  block-faces))
                           org-buffer))
                        (setq pos next))
                      ;; Add the face to the remaining part of the font.
                      (put-text-property (1- (+ start pos))
                                         end 'face
                                         (list :inherit block-faces) org-buffer)))
                  (add-text-properties
                   start end
                   '(font-lock-fontified t fontified t font-lock-multiline t))
                  (set-buffer-modified-p modified)))))

          (defun org-fontify-meta-lines-and-blocks-1 (limit)
            "Fontify #+ lines and blocks."
            (let ((case-fold-search t))
              (if (re-search-forward
                   "^\\([ \t]*#\\(\\(\\+[a-zA-Z]+:?\\| \\|$\\)\\(_\\([a-zA-Z]+\\)\\)?\\)[ \t]*\\(\\([^ \t\n]*\\)[ \t]*\\(.*\\)\\)\\)"
                   limit t)
                  (let ((beg (match-beginning 0))
                        (block-start (match-end 0))
                        (block-end nil)
                        (lang (match-string 7))
                        (beg1 (line-beginning-position 2))
                        (dc1 (downcase (match-string 2)))
                        (dc3 (downcase (match-string 3)))
                        end end1 quoting block-type ovl)
                    (cond
                     ((and (match-end 4) (equal dc3 "+begin"))
                      ;; Truly a block
                      (setq block-type (downcase (match-string 5))
                            quoting (member block-type org-protecting-blocks))
                      (when (re-search-forward
                             (concat "^[ \t]*#\\+end" (match-string 4) "\\>.*")
                             nil t)  ;; on purpose, we look further than LIMIT
                        (setq end (min (point-max) (match-end 0))
                              end1 (min (point-max) (1- (match-beginning 0))))
                        (setq block-end (match-beginning 0))
                        (when quoting
                          (org-remove-flyspell-overlays-in beg1 end1)
                          (remove-text-properties beg end
                                                  '(display t invisible t intangible t)))
                        (add-text-properties
                         beg end '(font-lock-fontified t font-lock-multiline t))
                        (add-text-properties beg beg1 '(face org-meta-line))
                        (org-remove-flyspell-overlays-in beg beg1)
                        (add-text-properties      ; For end_src
                         end1 (min (point-max) (1+ end)) '(face org-meta-line))
                        (org-remove-flyspell-overlays-in end1 end)
                        (cond
                         ((and lang (not (string= lang "")) org-src-fontify-natively)
                          (org-src-font-lock-fontify-block lang block-start block-end)
                          (add-text-properties beg1 block-end '(src-block t)))
                         (quoting
                          (add-text-properties beg1 (min (point-max) (1+ end1))
                                               (let ((face-name (intern (format "org-block-%s" lang))))
                                                 (append (and (facep face-name) (list face-name))
                                                         '(face org-block))))) ; end of source block
                         ((not org-fontify-quote-and-verse-blocks))
                         ((string= block-type "quote")
                          (add-text-properties beg1 (min (point-max) (1+ end1)) '(face org-quote)))
                         ((string= block-type "verse")
                          (add-text-properties beg1 (min (point-max) (1+ end1)) '(face org-verse))))
                        (add-text-properties beg beg1 '(face org-block-begin-line))
                        (add-text-properties (min (point-max) (1+ end)) (min (point-max) (1+ end1))
                                             '(face org-block-end-line))
                        t))
                     ((member dc1 '("+title:" "+author:" "+email:" "+date:"))
                      (org-remove-flyspell-overlays-in
                       (match-beginning 0)
                       (if (equal "+title:" dc1) (match-end 2) (match-end 0)))
                      (add-text-properties
                       beg (match-end 3)
                       (if (member (intern (substring dc1 1 -1)) org-hidden-keywords)
                           '(font-lock-fontified t invisible t)
                         '(font-lock-fontified t face org-document-info-keyword)))
                      (add-text-properties
                       (match-beginning 6) (min (point-max) (1+ (match-end 6)))
                       (if (string-equal dc1 "+title:")
                           '(font-lock-fontified t face org-document-title)
                         '(font-lock-fontified t face org-document-info))))
                     ((equal dc1 "+caption:")
                      (org-remove-flyspell-overlays-in (match-end 2) (match-end 0))
                      (remove-text-properties (match-beginning 0) (match-end 0)
                                              '(display t invisible t intangible t))
                      (add-text-properties (match-beginning 1) (match-end 3)
                                           '(font-lock-fontified t face org-meta-line))
                      (add-text-properties (match-beginning 6) (+ (match-end 6) 1)
                                           '(font-lock-fontified t face org-block))
                      t)
                     ((member dc3 '(" " ""))
                      (org-remove-flyspell-overlays-in beg (match-end 0))
                      (add-text-properties
                       beg (match-end 0)
                       '(font-lock-fontified t face font-lock-comment-face)))
                     (t ;; just any other in-buffer setting, but not indented
                      (org-remove-flyspell-overlays-in (match-beginning 0) (match-end 0))
                      (remove-text-properties (match-beginning 0) (match-end 0)
                                              '(display t invisible t intangible t))
                      (add-text-properties beg (match-end 0)
                                           '(font-lock-fontified t face org-meta-line))
                      t))))))

          (defface org-block-emacs-lisp
            `((t (:background "GhostWhite")))
            "Face for elisp src blocks")

          (defface org-block-python
            `((t (:background "WhiteSmoke")))
            "Face for python blocks")

          (defface org-block-ipython
            `((t (:background "AliceBlue")))
            "Face for python blocks")

          (defface org-block-sh
            `((t (:background "MintCream")))
            "Face for shell blocks")

          (defface org-block-latex
            `((t (:background "FloralWhite")))
            "Face for latex blocks")

          (defface org-block-plantuml
            `((t (:background "Ivory")))
            "Face for plantuml blocks"))
#+END_SRC

** Byte Compile

From this [[https://www.reddit.com/r/emacs/comments/4oxev7/how_should_i_start_unittesting_or_behaviortesting/][thread]]

#+BEGIN_SRC emacs-lisp
(defun byte-compile-current-buffer ()
  "`byte-compile' current buffer if it's emacs-lisp-mode and compiled file exists."
  (interactive)
  (when (and (eq major-mode 'emacs-lisp-mode)
             (file-exists-p (byte-compile-dest-file buffer-file-name)))
    (byte-compile-file buffer-file-name)))
#+END_SRC

** Minibuffer

Minibuffer window expands vertically as necessary to hold the text that you put
in the minibuffer

#+BEGIN_SRC emacs-lisp
(setq resize-mini-windows t) ;; was grow-only
#+END_SRC

** Scrooling
#+BEGIN_SRC emacs-lisp
(setq mouse-wheel-progressive-speed nil) ;; don't accelerate scrolling
(setq mouse-wheel-follow-mouse 't) ;; scroll window under mouse
#+END_SRC
** Dired Stuff
#+BEGIN_SRC emacs-lisp
  ;; dired sorts directories first from
  ;; http://www.emacswiki.org/emacs/DiredSortDirectoriesFirst 03Oct2012
  (defun mydired-sort ()
    "Sort dired listings with directories first."
    (save-excursion
      (let (buffer-read-only)
        (forward-line 2) ;; beyond dir. header
        (sort-regexp-fields t "^.*$" "[ ]*." (point) (point-max)))
      (set-buffer-modified-p nil)))

  (defadvice dired-readin
      (after dired-after-updating-hook first () activate)
    "Sort dired listings with directories first before adding mark."
    (mydired-sort))
  (put 'dired-find-alternate-file 'disabled nil)
#+END_SRC
** IBuffer
   #+BEGIN_SRC emacs-lisp
     ;; see http://www.emacswiki.org/emacs/IbufferMode 30Jul2014 Use human
     ;; readable Size column instead of original one
     (define-ibuffer-column size-h
       (:name "Size" :inline t)
       (cond
        ((> (buffer-size) 1000000) (format "%7.1fM" (/ (buffer-size) 1000000.0)))
        ((> (buffer-size) 100000) (format "%7.0fk" (/ (buffer-size) 1000.0)))
        ((> (buffer-size) 1000) (format "%7.1fk" (/ (buffer-size) 1000.0)))
        (t (format "%8d" (buffer-size)))))

     ;; Modify the default ibuffer-formats
     (setq ibuffer-formats
           '((mark modified read-only " "
                   (name 18 18 :left :elide)
                   " "
                   (size-h 9 -1 :right)
                   " "
                   (mode 16 16 :left :elide)
                   " "
                   filename-and-process)))

     ;; From ibuffer-vc.el
     (add-hook 'ibuffer-hook
               (lambda ()
                 (ibuffer-vc-set-filter-groups-by-vc-root)
                 (unless (eq ibuffer-sorting-mode 'alphabetic)
                   (ibuffer-do-sort-by-alphabetic))))

     ;; turn off ibuffer-show-empty-filter-groups
     ;; From http://martinowen.net/blog/2010/02/03/tips-for-emacs-ibuffer.html
     (setq ibuffer-show-empty-filter-groups nil)
   #+END_SRC
